@terraforming_cost_level_0 = 1250 #<-- same class terraforming
@terraforming_cost_level_1 = 2500 #<-- same category terraforming
@terraforming_cost_level_2 = 5000 #<-- different category terraforming
@terraforming_cost_level_3 = 10000 #<-- opposite category terraforming and uninhabitable to inhabitable terraforming
@terraforming_cost_level_4 = 15000 #<-- special terraforming, e.g. to gaia, machine world etc.

@terraforming_duration_level_0 = 900 #<-- same class terraforming
@terraforming_duration_level_1 = 1800 #<-- same category terraforming
@terraforming_duration_level_2 = 3600 #<-- different category terraforming
@terraforming_duration_level_3 = 5400 #<-- opposite category terraforming and uninhabitable to inhabitable terraforming
@terraforming_duration_level_4 = 7200 #<-- special terraforming, e.g. to gaia, machine world etc.

@gaiaCost = 7500
@gaiaTime = 3600

@highestCost = 12500
@highestTime = 9000

@machineCost = 10000
@machineTime = 7200

@hiveCost = 10000
@hiveTime = 7200

@lClusterCost = 1000

terraform_link = {
	to = "pc_machine"
	from = "pc_mzilli_shadow"

	resources = {
		category = terraforming

		cost = {
			energy = @machineCost
		}
	}

	duration = @machineTime

	potential = {
		OR = {
			has_ascension_perk = ap_machine_worlds
			has_origin = origin_machine
		}
	}

	ai_weight = {
		weight = 10
		modifier = {
			factor = 0
			from = {
				any_owned_species = {
					NOR = {
						has_trait = trait_machine_unit
						has_trait = trait_mechanical
					}
					is_sapient = yes
				}
			}
		}
	}
}

terraform_link = {
	to = "pc_hive"
	from = "pc_mzilli_shadow"
	
	resources = {
		category = terraforming

		cost = {
			energy = @hiveCost
		}
	}
	duration = @hiveTime

	potential = {
		OR = {
			has_origin = origin_hive_cx_hive_world
			has_ascension_perk = ap_hive_worlds
		}
	}

	effect = { from = { set_planet_entity = { entity = "shadow_world_01_entity" } } }

	ai_weight = {
		weight = 10
		modifier = {
			factor = 0
			from = {
				any_owned_pop = {
					NOT = { has_trait = trait_hive_mind }
				}
			}
		}
		modifier = {
			factor = 0
			from = {
				is_colony = no
			}
		}
	}
}